# Non-hierarchical PVL-Delta model for single subject
# Priors matched to hierarchical model

model {
  
  # Subject-level priors (matching hierarchical)
  w ~ dnorm(0, 1)              # Loss aversion
  A ~ dnorm(0, 1)              # Outcome sensitivity
  theta ~ dnorm(0, 1)T(0,)     # Inverse temperature (positive)
  a ~ dnorm(0, 1)              # Learning rate
  
  # Initialize trial 1 expected values (matching hierarchical)
  Ev[1,1] ~ dnorm(0, 0.01)
  Ev[1,2] ~ dnorm(0, 0.01)
  Ev[1,3] ~ dnorm(0, 0.01)
  Ev[1,4] ~ dnorm(0, 0.01)
  
  # Initialize p for trial 1
  p[1,1] <- .25
  p[1,2] <- .25
  p[1,3] <- .25
  p[1,4] <- .25
  
  for (t in 2:ntrials) {
    
    for (d in 1:4) {
      # Prospect Theory utility
      u[t, d] <- ifelse(X[t-1] < 0, -w * abs(X[t-1])^A, X[t-1]^A)
      
      # Delta rule update
      Ev_update[t, d] <- Ev[t-1, d] + (a * (u[t, d] - Ev[t-1, d]))
      Ev[t, d] <- ifelse(x[t-1] == d, Ev_update[t, d], Ev[t-1, d])
      
      # Softmax step 1
      exp_p[t, d] <- exp(theta * Ev[t, d])
    }
    
    for (d in 1:4) {
      # Softmax step 2
      p[t, d] <- exp_p[t, d] / sum(exp_p[t, ])
    }
    
    x[t] ~ dcat(p[t, ])
  }
}
