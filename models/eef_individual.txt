# EEF Model (Individual) - for posterior predictive checks
# Same equations as hierarchical but fit to single subject
#
# Equations:
#   V(t) = G(t)^theta - |L(t)|^theta
#   E_chosen(t) = (1-lambda)*E(t-1) + V(t)
#   E_unchosen(t) = (1-lambda)*E(t-1)
#   X_chosen(t) = 0
#   X_unchosen(t) = lambda*X(t-1) + (1-lambda)*phi

model {

  # uniform priors for individual-level estimation
  theta ~ dunif(1.0E-6, 1)   # value sensitivity
  lam   ~ dunif(0, 1)        # forgetting rate
  phi   ~ dunif(-5, 5)       # exploration bonus
  cons  ~ dunif(0, 5)        # consistency (pre-transform)

  # C = 3^c - 1
  C <- pow(3, cons) - 1

  # initialize at t=1
  for (d in 1:4) {
    Exploit[1,d] <- 0
    Explore[1,d] <- 0
    p[1,d] <- 0.25
    exp_p[1,d] <- 1
  }

  for (t in 2:ntrials) {

    # V(t) = Gain^theta - |Loss|^theta
    V[t] <- pow(Gain[t-1], theta) - pow(abs(Loss[t-1]), theta)

    for (d in 1:4) {

      # exploitation: decays then chosen gets V added
      Exploit_decay[t,d]  <- (1 - lam) * Exploit[t-1,d]
      Exploit_chosen[t,d] <- (1 - lam) * Exploit[t-1,d] + V[t]
      Exploit[t,d] <- ifelse(d == x[t-1], Exploit_chosen[t,d], Exploit_decay[t,d])

      # exploration: chosen resets, unchosen drifts toward phi
      Explore_unchosen[t,d] <- lam * Explore[t-1,d] + (1 - lam) * phi
      Explore[t,d] <- ifelse(d == x[t-1], 0, Explore_unchosen[t,d])

      # Q = E + X
      TotalVal[t,d] <- Exploit[t,d] + Explore[t,d]
      a[t,d] <- C * TotalVal[t,d]
    }

    # stable softmax
    amax[t] <- max(a[t,1:4])

    for (d in 1:4) {
      exp_p[t,d] <- exp(a[t,d] - amax[t])
    }

    denom[t] <- sum(exp_p[t,1:4])

    for (d in 1:4) {
      p[t,d] <- exp_p[t,d] / denom[t]
    }

    x[t] ~ dcat(p[t,1:4])
  }
}